#!/usr/bin/perl -w 

use strict;
use Getopt::Std;
use Data::Dumper;
use File::Find;
use DBI();
use Term::ReadPassword;

sub untarrepodb{ #repodb
    my ($repodb) = @_;
    my $dir = "/tmp/$repodb".".$$";
    die $! unless mkdir $dir;
    die "No such file:$repodb" unless -e $repodb;
    qx"tar xzf $repodb -C $dir";
    return $dir;
}

sub rrm { #file
    qx/rm -rf $_[0]/;
}

sub getcontents { #file          # puts a textfile to an ARRAY
    my $d = shift;
    die "$! $d" unless open FILE, $d;
    my @lines = <FILE>;
    close FILE;
    return @lines;
}

sub getrepodbdata { #what from      # takes '%foo%' and gives a SCALAR
    my ($what, $from) = (shift, join '', @_);
    my ($out) = $from =~ /$what.(.*?).(:?.%|\Z)/s;
    $out =~ s/\n/ /g if $out;
    return $out;
}

sub lengthb255{ # SCALAR
    my $p = shift;
    return undef unless $p;
    (length $p > 255) ? undef : $p;
}

sub readdata { #string
    my $str = shift;
    my $inp = '';
    while ($inp eq ''){print $str;$inp = <STDIN>; chomp $inp;}
    return $inp;
}

sub HELP_MESSAGE{
    print <<END
fdb2db [-v] -f file -a architecture -r name -e version -b dir [-p] 
       [-u user] [-i] [-t table] [-d database] [-h host] [-c packagename]

        -c packagename
	-f .fdb file
	-a architecture
	-r name of repository
	-e version of distribution
	-b buildscript directory
	-p asks for a password
	-u user of database
	-t name of table in the database
	-d name of database
	-h host of the database
	-s its a subpkg
	-n parentpkg
	-l uploader
	-i be interactive
	-v be verbose

END
}

my %metaverify = (ARCH => 'architecture',
		  DESC => 'description',
		  MD5SUM => 'md5sum',
		  SHA1SUM => 'sha1', 
		  FILELIST => 'filelist',
#		  M8R => 'maintainer',
		  VERSION => 'version',
		  PKGREL => 'release',
		  );

my %opts;
getopts('r:e:b:pu:vit:d:h:f:a:c:sn:l:', \%opts);

HELP_MESSAGE && exit unless %opts;

die 'Specify a repositoryname!' unless my $repo = $opts{r};
die 'Specify a filename!' unless my $fdbfile = $opts{f};
die 'Specify a version string!' unless my $fwver = $opts{e};
die 'Specify the FrugalBuild directory!' unless my $fbdir = $opts{b};
die 'Specify the architecture!' unless my $arch = $opts{a};
die 'Invalid architecture!' unless $arch =~ /i686|x86_64|ppc/;

my $warn = '';
my $user;
my $table;
my $host;
my $database;
$user = $opts{u} or $user = '';
$table = $opts{t} or $table = '';
$host = $opts{h} or $host = 'localhost';
$database = $opts{d} or $database = '';

my $db = 'mysqldb';

die 'Specify a table in the database!' if $table eq '';
die 'Specify a database name!' if $database eq '';
my $passw = read_password('password: ') if $opts{p};

my $onepkg = $opts{c};
my $subpkg = $opts{s};
my $parentpkg = $opts{n};
my $uploader = $opts{l};

### ERROR checking for 'sn:' opts

$db->connect({database => $database,
	      host => $host,
	      user => $user,
	      table => $table,
	      password => $passw,
	  });
$passw = '';

my $pkgdir = untarrepodb $fdbfile;
opendir DBDIR, $pkgdir;

while (my $pkg = readdir DBDIR){   #pkg is a dir, pkg =~ name-ver-rel 

    next if $pkg =~ /^\.{1,2}$/;
    if ($onepkg){
      my ($pkgn, $pkgv, $pkgr) = $pkg =~ /(.*)-(.*)-(.*)/;
      unless ($pkgn =~ /^\Q$onepkg\E\Z/) {print "Skipping $pkg\n" if $opts{v}; next;}
    }
    my %pkgdata;
    ($pkgdata{FWVER}, $pkgdata{REPO}) = ($fwver, $repo);
    my @contents = getcontents "$pkgdir/$pkg/desc";
   
    for my $link (qw/NAME VERSION DESC CSIZE MD5SUM SHA1SUM GROUPS REPLACES/){   
	$pkgdata{$link} = getrepodbdata '%'.$link.'%', @contents;
    }

    $warn .= "No sha1sum nor md5sum determined for $pkgdata{NAME}\n" unless $pkgdata{MD5SUM} or $pkgdata{SHA1SUM};

    for my $link (qw/DESC CSIZE/){
	$warn .= "No \L$link\E determined for $pkgdata{NAME}\n" unless $pkgdata{$link};
    }

    ($pkgdata{VERSION}, $pkgdata{PKGREL}) = split '-', $pkgdata{VERSION}; # VERSION =~ ver-rel

#    ($pkgdata{ARCH}) =  qx/tar xzOf $pkg.fpm .PKGINFO/ =~ /^arch = (.*)$/m or $warn .= "No arthitecture determined for $pkgdata{NAME}\n";  ## the old way, maybe usefull later
    $pkgdata{ARCH} = $arch;

    print "$pkgdata{NAME}:\n" if $opts{v};

    do{print " Not enough data!\n" if $opts{v}; next;} unless $pkgdata{NAME}||$pkgdata{VERSION}|| $pkgdata{PKGREL}||$pkgdata{FWVER} ||$pkgdata{ARCH};
    do {print " No need to update\n" if $opts{v}; next;} unless $db->needupdate($pkgdata{NAME}, $pkgdata{VERSION}, $pkgdata{PKGREL}, $pkgdata{FWVER},$pkgdata{ARCH});

    @contents = getcontents "$pkgdir/$pkg/depends";
    for my $link (qw/PROVIDES CONFLICTS DEPENDS/){
	$pkgdata{$link} = getrepodbdata '%'.$link.'%', @contents;
    }

    unless ($pkgdata{REPLACES}){
	$pkgdata{REPLACES} = getrepodbdata 'REPLACES', @contents;
    }

    $pkgdata{FILELIST} = "/tmp/FILELIST.$pkg-$arch";
    my $comm = "tar x".(qx "file $pkg-$arch.fpm" =~ /bzip2/ ? 'j' : 'z')."Of $pkg-$arch.fpm .FILELIST > /tmp/FILELIST.$pkg-$arch";
    qx/$comm/;
    
    my $buildscript;
    find sub {
	$buildscript = $File::Find::name if $File::Find::name =~ m"\Q$pkgdata{NAME}\E/FrugalBuild\Z";
	}, $fbdir;

    if (!$buildscript){
	if ($opts{i}){
	    $buildscript = readdata "Full path to the FrugalBuild script of $pkgdata{NAME}:";
	} else {
	    unless ($subpkg) {
		$warn .= "No FrugalBuild found to $pkgdata{NAME}\n" ;
		$pkgdata{NVALID} = 1;
	    }
	}
    }

    ($pkgdata{M8R}) = qx"cat $buildscript" =~ /^\#.?Maintainer: (.*)$/m if $buildscript && -e $buildscript;

    $pkgdata{PARENT} = $parentpkg;
    $pkgdata{UPLOADER} = $uploader;

##### VERIFY INFO
########################sha1 or md5???????             ???????????
    for my $key (keys %metaverify) {
      next if $key eq 'SHA1SUM' || $key eq 'MD5SUM';
      if (not $pkgdata{$key}) {
	if ($opts{i}){
	  $pkgdata{$key} = readdata "\u$metaverify{$key} of $pkgdata{NAME}: ";
	} else {
	  $warn .= "No $metaverify{$key} determined for $pkgdata{NAME}\n";
	  $pkgdata{NVALID} = 1;
	}
      }	
    }
    
    if (not $pkgdata{GROUPS}) {
	do{do{($pkgdata{GROUPS}) = $buildscript =~ m'.*/(.*?)/.*?/FrugalBuild'} if $buildscript} or do{
	    if ($opts{i}){
		$pkgdata{GROUPS} = readdata "Groups of $pkgdata{NAME}: ";
	    } else {
		$warn .= "No groups determined for $pkgdata{NAME}\n";
		$pkgdata{NVALID} = 1;
	    }
	}
    }

    my $action = $db->store(\%pkgdata) unless $pkgdata{NVALID};
    print " $warn" if $warn; $warn = '';
    print " $action\n" if $opts{v} and  not $pkgdata{NVALID};

    rrm $pkgdata{FILELIST};
    %pkgdata = ();
}

$db->disconnect;
closedir DBDIR;
rrm $pkgdir;


package mysqldb;

my $dbh;
my $dbtable;

sub connect{ # datahash
    my ($self,$datas) = @_;
    $dbtable = $datas->{table};
    $dbh = DBI->connect("DBI:mysql:database=$datas->{database};host=$datas->{host}",
			$datas->{user}, $datas->{password},
			{'RaiseError' => 1}
			);
}

sub needupdate {
    shift;
    my($name,$ver,$rel,$fwver,$arch) = @_;
    my $sth = $dbh->prepare("SELECT id FROM $dbtable where pkgname='$name' and pkgver='$ver' and pkgrel='$rel' and fwver='$fwver' and arch='$arch'");
    $sth->execute();
    do {$sth->finish(); return 0;} if $sth->fetchrow_hashref();
    $sth->finish();
    return 1;
}

sub isold {
    my($name,$fwver,$arch) = @_;
    my $sth = $dbh->prepare("SELECT id FROM $dbtable where pkgname='$name' and fwver='$fwver' and arch='$arch'");
    $sth->execute();
    do {$sth->finish(); return 1;} if $sth->fetchrow_hashref();
    $sth->finish();
    return 0;
}


sub store{
    shift;
    my $data = shift;
    my $values;
    
    for my $key (qw/NAME PARENT VERSION PKGREL GROUPS PROVIDES DEPENDS CONFLICTS REPLACES CSIZE ARCH DESC M8R UPLOADER MD5SUM SHA1SUM FWVER REPO FILELIST/){
	$data->{$key} = 'NULL' unless $data->{$key};
    }
    for my $key (qw/NAME VERSION GROUPS PROVIDES DEPENDS CONFLICTS REPLACES ARCH DESC M8R UPLOADER FWVER REPO FILELIST/){
	$data->{$key} =~ s/'/\\'/g if $data->{$key};
    }
    
    if (isold($data->{NAME}, $data->{FWVER}, $data->{ARCH})){
	my $set = '`pkgname`=';
	for my $key (qw/NAME `parent` PARENT `pkgver` VERSION `pkgrel` PKGREL `groups` GROUPS `provides` PROVIDES `depends` DEPENDS `conflicts` CONFLICTS `replaces` REPLACES `csize` CSIZE `arch` ARCH `desc` DESC `maintainer` M8R `uploader` UPLOADER `md5` MD5SUM `sha1` SHA1SUM `fwver` FWVER `repo` REPO `files`/){
	    
	    $set .= ($data->{$key}) ? "'$data->{$key}'," : "$key=";
	}
	$set .= "LOAD_FILE('$data->{FILELIST}')";
	$set .= ',`updated`=now()'; #mysql bug?
	$dbh->do("UPDATE $dbtable set $set where pkgname='$data->{NAME}' and fwver='$data->{FWVER}' and arch='$data->{ARCH}'");
	return 'updated';
    } else {
	for my $key (qw/NAME PARENT VERSION PKGREL GROUPS PROVIDES DEPENDS CONFLICTS REPLACES CSIZE ARCH DESC M8R UPLOADER MD5SUM SHA1SUM FWVER REPO/){
	    $values .= "'$data->{$key}',";
	}
	$values .= "LOAD_FILE('$data->{FILELIST}')";
	$dbh->do("INSERT INTO $dbtable (`pkgname`,`parent`,`pkgver`,`pkgrel`,`groups`,`provides`,`depends`,`conflicts`,`replaces`,`csize`,`arch`,`desc`,`maintainer`,`uploader`,`md5`,`sha1`,`fwver`,`repo`,`files`,`updated`) VALUES ($values, now())");
	return 'inserted';
    }
}

sub disconnect{
    $dbh->disconnect();
}



1;


__END__

         pkgname   	varchar(255)    NAME r
         parent         varchar(255)    PARENT opts
	 pkgver  	varchar(255) 	VERSION r
	 pkgrel  	tinyint(4) 	PKGREL r
	 groups  	varchar(255) 	GROUPS r
	 provides  	varchar(255) 	PROVIDES r/d             r/d <=> repodb/depends file
	 depends  	varchar(255) 	DEPENDS r/d		r <=> repo.db/desc
	 conflicts  	varchar(255) 	CONFLICTS r/d
	 replaces  	varchar(255) 	REPLACES r
	 csize  	int(11) 	CSIZE r
	 arch  		varchar(255) 	ARCH .PKGINFO
	 desc  		varchar(255) 	DESC r
	 maintainer  	varchar(255) 	M8R FrugalBuild
	 uploader  	varchar(255) 	UPLOADER opts
	 md5  		varchar(32) 	MD5SUM r 
	 fwver  	varchar(255) 	FWVER Parameter
	 repo  		varchar(255) 	REPO Parameter
	 files  	mediumtext 	FILELIST .FILELIST


=head1 NAME

fdb2db - refresh a Frugalware database from a repository

=head1 SYNOPSIS

fdb2db [-v] -f file -r name -e version -b dir [-p] [-u user] [-i] [-t table] [-d database] [-h host]

=head1 DESCRIPTION

Scans the current directory for packages and a repository database,
then syncronizes a database. See DATABASE for the structure.

=head1 OPTIONS

=over 4

=item B<-c packagename>

Specify a package to update

=item B<-f file>

The .fdb file of the repository.

=item B<-a arch>

The architecture ex. i686, x86_64
    
=item B<-r name>

The name of the repository.

=item B<-e version>

The version string of the disrtibution,

=item B<-b dir>

A parent directory of the buildscripts. ex. ../source/

=item B<-p>

Ask for a password if your database engine require.

=item B<-u user>

The user of the database if your engine require.

=item B<-t table>

The table in your database if required.

=item B<-d database>

The name of the database if required.

=item B<-h host>

The host where the database is located, localhost by default.

=item B<-s>

The given package is a subpackage.

=item B<-n parentpkg>

The parent package of the given.

=item B<-l uploader>

The nick of ther person who uploaded the package.

=item B<-i>

Be interactive. This will ask you about missing datas if nessesary.

=item B<-v>

Be verbose.

=head1 DATABASE

         pkgname   	varchar(255)    
	 pkgver  	varchar(255) 	
	 pkgrel  	tinyint(4) 	
	 groups  	varchar(255) 	
	 provides  	varchar(255) 	
	 depends  	varchar(255) 	
	 conflicts  	varchar(255) 	
	 replaces  	varchar(255) 	
	 csize  	int(11) 	
	 arch  		varchar(255) 	
	 desc  		varchar(255) 	
	 maintainer  	varchar(255) 	
	 md5  		varchar(32) 	
	 fwver  	varchar(255) 	
	 repo  		varchar(255) 	
	 files  	mediumtext 	
	 updated  	timestamp       


=head1 AUTHOR

Zsolt Szalai <xbit@frugalware.org>

=head1 REPORTING BUGS

Report bugs to <xbit@frugalware.org>

=head1 COPYRIGHT                                                                
                                                                                
May be copied and modified under the terms of the GNU General Public     
License v2.                                                                     
                                                                                
=cut
