# Last Modified: Fri, 05 Aug 2005 20:16:25 +0200
# Compiling Time: 1.86 SBU
# Maintainer: Christian Hamar alias krix <krics@linuxforum.hu>

pkgname=boost
pkgver=1.32.0
pkgrel=2
pkgdesc="Boost Libraries for C++"
url="http://boost.org/"
depends=('libstdc++' 'python')
makedepends=('boost-jam')
groups=('devel-extra')
archs=('i686' 'x86_64')
up2date="lynx -dump 'http://sourceforge.net/project/showfiles.php?group_id=7586' |grep boost_[0-9.\]*.tar.bz2|sed -ne 's/.*-\(.*\).t.*/\1/;1 p'"
source=(http://dl.sourceforge.net/$pkgname/boost_1_32_0.tar.bz2 \
	boost-gcc4-remove-warns.patch)
sha1sums=('bb193b381d8e1c8321ee7d77f01c88837edc3e56' \
          '5f29922009ee90069ceeb14930331c4355c19ebb')

build() {
	Fcd boost_1_32_0
	Fpatchall
	
	BOOST_TOOLSET="gcc"
	TOOLSET_NAME="gcc"
	SOSUFFIX="so"
	BOOSTJAM=/usr/bin/bjam
	BUILD="release <runtime-link>dynamic"
	BUILD="${BUILD} <threading>single"
	PYTHON_VERSION=`python -V 2>&1 |sed 's,.* \([0-9]\.[0-9]\)\(\.[0-9]\)\?.*,\1,'`

	${BOOSTJAM} -sBOOST_ROOT="$Fsrcdir" \
	    -sPYTHON_ROOT=/usr \
	    -sPYTHON_VERSION=${PYTHON_VERSION} \
	    -sTOOLS=${BOOST_TOOLSET} \
	    -sBUILD="${BUILD}" \
	    --prefix=$Fdestdir/usr \
	    --layout=system

	${BOOSTJAM} -sBOOST_ROOT="$Fsrcdir" \
	    -sPYTHON_ROOT=/usr \
	    -sPYTHON_VERSION=${PYTHON_VERSION} \
	    -sTOOLS=${BOOST_TOOLSET} \
	    -sBUILD="${BUILD}" \
	    --prefix=$Fdestdir/usr \
	    --layout=system install || return 1
	
	cd $Fsrcdir/boost_1_32_0/tools/build/
	Fmkdir usr/share/boost
	cp -a b* index.html v1/ v2/ $Fdestdir/usr/share/boost/

        cd $Fdestdir/usr/lib

### Braindead thing ....

	for fn in `ls -1 *.${SOSUFFIX}| cut -d- -f1 | sed 's/\.so//g' | sort | uniq`; do
		Fln "$fn.${SOSUFFIX}" "/usr/lib/$fn-gcc.${SOSUFFIX}"
	done

	for fn in `ls -1 *.a| cut -d- -f1 | sed 's/\.a//g' | sort | uniq`; do
		Fln "$fn.a" "/usr/lib/$fn-gcc.a"
	done
}

# vim: ft=sh
