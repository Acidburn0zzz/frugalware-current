# Compiling time: 1.67 SBU
# Maintainer: Christian Hamar alias krix <krics@linuxforum.hu>
# Contributor: Bence Nagy <nagybence@tipogral.hu>

pkgname=claws-mail
pkgver=3.6.1
pkgrel=1
pkgdesc="The extended version of Sylpheed, a GTK+ based, lightweight, and fast e-mail client"
groups=('xapps-extra')
archs=('i686' 'x86_64' 'ppc')
options=('scriptlet')
depends=('openssl' 'aspell' 'pilot-link>=0.12.1'  \
	'gtk+2>=2.14.4-2' 'gpgme' 'startup-notification' 'libldap' \
	'spamassassin' 'curl' 'libetpan>=0.57' \
	'hicolor-icon-theme' 'glib2>=2.18.2-1' 'db>=4.7.25')
makedepends=('openldap')
_F_sourceforge_realname="Claws Mail"
_F_sourceforge_ext=".tar.bz2"
_F_sourceforge_dirname="sylpheed-claws"
conflicts=('sylpheed-claws')
replaces=('sylpheed-claws')
provides=('sylpheed-claws')
Finclude sourceforge
url="http://www.claws-mail.org/"

build() {
	Fmake \
		--enable-aspell --enable-ldap \
		--enable-spamassassin-plugin
	cd tools || Fdie
	make || Fdie
	for tool in *.pl *.py *.sh tb2* update-po uudec; do
    		Fexerel /usr/lib/claws-mail/tools/${tool}
	done
	Ffilerel /usr/lib/claws-mail/tools/multiwebsearch.conf
	cd .. || Fdie
	Ffilerel claws-mail-64x64.png /usr/share/pixmaps/claws-mail.png
	Fmakeinstall
	Ffilerel claws-mail.png /usr/share/icons/hicolor/48x48/apps/claws-mail.png
	Ffilerel claws-mail.desktop /usr/share/applications/claws-mail.desktop
}

sha1sums=('5894b6e533a151ce8e4be6445e117cd40a0f63b9')
# optimization OK
