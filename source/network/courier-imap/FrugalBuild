# Last Modified: Mon, 16 May 2005 19:44:11 +0200
# Compiling Time: ~35 minutes
# Maintainer: Zoltan Kiss <djsmiley@inflame.hu>

pkgname=courier-imap
pkgver=4.0.3
pkgrel=1
pkgdesc="The Courier IMAP and POP3 server"
depends=('courier-authlib')
groups=('network')
backup=('etc/courier/imapd','etc/courier/pop3d','etc/courier/imapd-ssl','etc/courier/pop3d-ssl')
makedepends=('postfix' 'openldap' 'mysql' 'postgresql' 'courier-authlib')
up2date="lynx -dump 'http://www.courier-mta.org/download.php'|grep imap|sed -n 's/.*-\(.*\)\.t.*/\1/;1 p'"
source=(http://dl.sourceforge.net/courier/$pkgname-$pkgver.tar.bz2 \
	rc.courier-imapd rc.courier-imapd-ssl rc.courier-pop3d \
	rc.courier-pop3d-ssl README.frugalware)
md5sums=('1bbe6544dc39ba14e6c71589c7f74337' '6380175cba374a484841572b78f637fa' \
	 'aaf1cee60a88131a06d4ee49901cb63b' 'bfcf5370c2a7a2fb5f8922385c04b2a3' \
	 'f969dc38e5256558c03db811910d0fdf' '2a401a97df9ecd6f61cfdc545ab5e589')

build()
{
	Fbuild --mandir=/usr/share/man \
                --with-piddir=/var/run/courier \
		--sysconfdir=/etc/courier \
		--libexecdir=/usr/lib/courier \
		--datadir=/usr/lib/courier \
		--localstatedir=/var/lib/courier \
		--sbindir=/usr/sbin \
		--with-mailuser=daemon \
		--with-mailgroup=daemon \
		--without-socks \
		--enable-workarounds-for-imap-client-bugs \
		--without-authpam \
		--with-authmysql \
		--with-mysql-includes=/usr/include/mysql \
		--with-mysql-libs=/usr/lib \
		--with-authpgsql \
		--with-pgsql-includes=/usr/include/postgresql \
		--with-pgsql-libs=/usr/lib \
		--with-authshadow \
		--with-authvchkpw \
		--with-authdaemonvar=/var/run/courier/authdaemon \
		--with-authldap \
		--with-authcram \
		--with-db=gdbm \
		--enable-userdb \
		--enable-syslog=1 \
		--enable-unicode \
		--disable-root-check || return 1
	
	# remove original courier rc scripts (we don't need them :P )
	# this block is only required if we compile courier-imap source package
	rm $startdir/pkg/usr/lib/courier/imapd.rc							  
	rm $startdir/pkg/usr/lib/courier/imapd-ssl.rc							  
	rm $startdir/pkg/usr/lib/courier/pop3d.rc							  
	rm $startdir/pkg/usr/lib/courier/pop3d-ssl.rc							  

        # config files
	Fmkdir /usr/share/doc/$pkgname-$pkgver/examples
	Fmkdir /usr/share/doc/$pkgname-$pkgver/examples/ssl
	mv $startdir/pkg/etc/courier/quotawarnmsg.example $startdir/pkg/usr/share/doc/$pkgname-$pkgver/examples/
	mv $startdir/pkg/etc/courier/imapd.cnf $startdir/pkg/usr/share/doc/$pkgname-$pkgver/examples/ssl/
	mv $startdir/pkg/etc/courier/pop3d.cnf $startdir/pkg/usr/share/doc/$pkgname-$pkgver/examples/ssl/
	cd $startdir/pkg/etc/courier/
	for f in *.dist; do cp $f `basename $f .dist`; done
	mv $startdir/pkg/etc/courier/*.dist $startdir/pkg/usr/share/doc/$pkgname-$pkgver/examples/

	Fdoc README.frugalware
		
	#initscripts
	Frcd courier-imapd
	Frcd courier-imapd-ssl
	Frcd courier-pop3d
	Frcd courier-pop3d-ssl
}

# vim: ft=sh
