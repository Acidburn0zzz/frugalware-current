# Last modified: Sun, 12 Jun 2005 12:06:51 +0200
# Compiling time: ~2,1 hours
# Maintainer: IroNiQ <iron@ironiq.hu>
# Modified by Laszlo Dvornik <dvornik@gnome.hu>

pkgname=thunderbird
pkgver=1.0.2
pkgrel=2
pkgdesc="Mozilla Thunderbird mail and newsgroup client"
url="http://www.mozilla.org/projects/thunderbird/"
depends=('gtk+2' 'libidl')
makedepends=('zip')
groups=('xapps')
archs=('i686')
install=$pkgname.install
up2date="wget -O - -q ftp://ftp.mozilla.org/pub/mozilla.org/thunderbird/releases/|grep '>[0-9\.]*/'|sed -n 's|.*>\([0-9\.]*\)/.*|\1|;$ p'"
source=(http://ftp.mozilla.org/pub/mozilla.org/$pkgname/releases/$pkgver/source/$pkgname-$pkgver-source.tar.bz2 \
	00-$pkgname-0.8-progname.patch \
	01-$pkgname-check-default-mailer.patch \
	02-$pkgname-locale.patch \
	03-$pkgname-myspell.patch \
	04-$pkgname-1.0-quiet-extensions.patch \
	05-$pkgname-1.0-fix-extension-manager.patch \
	06-$pkgname-1.0.2-js-vulnerability.patch \
	07-$pkgname-0.7.3-freetype-compile.patch \
	08-$pkgname-0.7.3-gnome-uriloader.patch \
	09-firefox-1.0-prdtoa.patch \
	10-$pkgname-0.8-default-applications.patch \
	11-firefox-PR1-gnome-vfs-default-app.patch \
	12-$pkgname-0.8.0-stack-direction.patch \
	13-firefox-PR1-gtk-file-chooser-more-fixes.patch \
	14-$pkgname-0.8.0-pkgconfig.patch \
	15-$pkgname-1.0-useragent.patch \
	mozilla-$pkgname.desktop \
	$pkgname.desktop \
	$pkgname-frugalware-default-prefs.js \
	open-browser.sh \
	$pkgname.install \
	$pkgname.png \
	$pkgname-rebuild-databases.pl.in \
	$pkgname.sh.in)
md5sums=('9e5b8a3edb3ced400e769dc2faa45317' \
	'b8820072bd97d501b980f432730ab30c' \
	'8421a59ba73b8c872d0dc0736687383d' \
	'3fa0eb0b2142942571677ee439114f8d' \
	'a72bc6693d742b52d0f595211e99159b' \
	'e26956632643894acd8927586889aae5' \
	'885c18150df5998d9dfdab5c8a2c6d07' \
	'e7f43c3e1f7dcf8d562705856c1b9c43' \
	'd53fccb9616e19ac0302cf6c65b19d00' \
	'c8efa922e35563165090a7c563d00d3d' \
	'5effa3338ee98e4c2ff4aec5c2035a85' \
	'2b80e997bac34ff90af7b64d3f66c144' \
	'1a719de92d68be02ba18f7cb59e2ec46' \
	'5b28e0fe4f229230cc0fea5afcaae413' \
	'54d3aaec464df6aeaf5db8ca45429517' \
	'ddeb23a4941dd256d6a13a6075392ce4' \
	'311f6320beb546b9cd39ac899aee7f22' \
	'61dc5e1c709d2281afd8cf1f7c942cb2' \
	'b9c4354f4a755b700f0aac8935f7b2d5' \
	'40202bf8c657cb29fb0f22cc133c2667' \
	'57b20458581d5ce253f8c29084e43120' \
	'40a2550201f75f351a7bdd4cba9b1d72' \
	'22e9921d9d89e8e9ec4a849dd6be7d68' \
	'1d063924b9c473d655a78cbb845ab330' \
	'65f982e2a07a64fbfc053b48070a2817')

build() {

	cd $startdir/src/mozilla
	export MOZ_THUNDERBIRD="1"
	patch -p1 < $startdir/src/00-$pkgname-0.8-progname.patch || return 1
	patch -p1 < $startdir/src/01-$pkgname-check-default-mailer.patch || return 1
	patch -p1 < $startdir/src/02-$pkgname-locale.patch || return 1
	patch -p1 < $startdir/src/03-$pkgname-myspell.patch || return 1
	patch -p1 < $startdir/src/04-$pkgname-1.0-quiet-extensions.patch || return 1
	patch -p1 < $startdir/src/05-$pkgname-1.0-fix-extension-manager.patch || return 1
	patch -p1 < $startdir/src/06-$pkgname-1.0.2-js-vulnerability.patch || return 1
	patch -p0 < $startdir/src/07-$pkgname-0.7.3-freetype-compile.patch || return 1
	patch -p1 < $startdir/src/08-$pkgname-0.7.3-gnome-uriloader.patch || return 1
	patch -p0 < $startdir/src/09-firefox-1.0-prdtoa.patch || return 1
	patch -p1 < $startdir/src/10-$pkgname-0.8-default-applications.patch || return 1
	patch -p1 < $startdir/src/11-firefox-PR1-gnome-vfs-default-app.patch || return 1
	patch -p0 < $startdir/src/12-$pkgname-0.8.0-stack-direction.patch || return 1
	patch -p0 < $startdir/src/13-firefox-PR1-gtk-file-chooser-more-fixes.patch || return 1
	patch -p0 < $startdir/src/14-$pkgname-0.8.0-pkgconfig.patch || return 1
	patch -p0 < $startdir/src/15-$pkgname-1.0-useragent.patch || return 1

	./configure --prefix=/usr \
	--with-system-jpeg \
	--with-system-zlib \
	--with-system-png \
	--with-system-mng \
	--disable-calendar \
	--enable-xft \
	--disable-pedantic \
	--disable-svg \
	--enable-mathml \
	--enable-xsl \
	--enable-crypto \
	--disable-xinerama \
	--disable-xprint \
	--with-pthreads \
	--with-default-mozilla-five-home=/usr/lib/thunderbird-$pkgver \
	--disable-jsd \
	--disable-accessibility \
	--disable-profilesharing \
	--disable-necko-disk-cache \
	--disable-activex-scripting \
	--disable-installer \
	--disable-activex \
	--disable-debug \
	--disable-tests \
	--disable-strip \
	--disable-dtd-debug \
	--disable-logging \
	--enable-reorder \
	--enable-strip-libs \
	--enable-xterm-updates \
	--disable-toolkit-qt \
	--disable-toolkit-xlib \
	--enable-extensions=wallet,spellcheck,xmlextras,webservices \
	--enable-necko-protocols=http,file,jar,viewsource,res,data \
	--enable-image-decoders=png,gif,jpeg,bmp \
	--enable-toolkit-gtk2 \
	--enable-default-toolkit=gtk2 \
	--disable-toolkit-gtk \
	--enable-ipv6 \
	--enable-freetype2 \
	--enable-single-profile \
	--disable-official-branding

	# Let jars get compressed.
	perl -p -i -e 's|\-0|\-9|g' config/make-jars.pl

	make || return 1

	make DESTDIR=$startdir/pkg install

	mkdir -p $startdir/pkg/usr/lib/$pkgname-$pkgver/chrome/lang
	cp $startdir/pkg/usr/lib/$pkgname-$pkgver/chrome/installed-chrome.txt \
		$startdir/pkg/usr/lib/$pkgname-$pkgver/chrome/lang/installed-chrome.txt

	# Compatibility.
	cd $startdir/pkg/usr/include
	ln -s $pkgname-$pkgver $pkgname
	cd $startdir/pkg/usr/lib
	ln -s $pkgname-$pkgver $pkgname
	cd $startdir/pkg/usr/share/idl
	ln -s $pkgname-$pkgver $pkgname

	cd $startdir/src/mozilla
	mkdir -p $startdir/pkg/usr/include/$pkgname-$pkgver/nss
	cp -Lf dist/private/nss/*.h dist/public/nss/*.h $startdir/pkg/usr/include/$pkgname-$pkgver/nss
	chown -R root:root $startdir/pkg/usr/include/$pkgname-$pkgver/nss

	# Install thunderbird-rebuild-databases.pl
	sed "s/VERSION/$pkgver/" $startdir/src/$pkgname-rebuild-databases.pl.in > \
		$startdir/pkg/usr/lib/$pkgname-$pkgver/$pkgname-rebuild-databases.pl
	chmod 755 $startdir/pkg/usr/lib/$pkgname-$pkgver/$pkgname-rebuild-databases.pl

	# Install open-browser.sh.
	install -D -m755 -o root -g root $startdir/src/open-browser.sh \
		$startdir/pkg/usr/lib/$pkgname-$pkgver/open-browser.sh

	# Install thunderbird.sh.
	mv $startdir/pkg/usr/bin/$pkgname $startdir/pkg/usr/lib/$pkgname-$pkgver/
	sed "s/VERSION/$pkgver/" $startdir/src/$pkgname.sh.in > \
		$startdir/pkg/usr/bin/$pkgname
	chmod 755 $startdir/pkg/usr/bin/$pkgname

	# Install thunderbird-frugalware-default-prefs.js.
	sed "s/FW_VR/$pkgver-$pkgrel/" $startdir/src/$pkgname-frugalware-default-prefs.js > \
		$startdir/pkg/usr/lib/$pkgname-$pkgver/greprefs/all-frugalware.js
	cp $startdir/pkg/usr/lib/$pkgname-$pkgver/greprefs/all-frugalware.js \
		$startdir/pkg/usr/lib/$pkgname-$pkgver/defaults/pref/all-frugalware.js
	chmod 755 $startdir/pkg/usr/bin/$pkgname

	# Install icon for the menu file.
	install -D -m644 -o root -g root $startdir/src/$pkgname.png \
		$startdir/pkg/usr/share/pixmaps/$pkgname.png

	# Install GNOME menu file.
	install -D -m644 -o root -g root $startdir/src/$pkgname.desktop \
		$startdir/pkg/usr/share/applications/$pkgname.desktop

	# Install KDE menu file.
	install -D -m644 -o root -g root $startdir/src/mozilla-$pkgname.desktop \
		$startdir/pkg/usr/share/applnk/Internet/mozilla-$pkgname.desktop
}

# vim: ft=sh
