# Compiling time: 0.12 SBU
# Maintainer: bouleetbil <bouleetbil@frogdev.info>

pkgname=gnome-shell-extensions
pkgver=3.0.2
pkgrel=1
pkgdesc="Modify and extend GNOME Shell functionality and behavior"
depends=('gnome-shell>=3.0.1')
makedepends=('intltool' 'gnome-doc-utils' 'gnome-common' \
	'gobject-introspection' 'krb5')
groups=('gnome')
archs=('i686' 'x86_64' 'ppc')
_F_gnome_git="n"
_F_gnome_glib="y"
Finclude gnome gnome-scriptlet
sha1sums=('d7473bd7276569e49fed6407fb2da0b50c84aa3c')
if [ "$_F_gnome_git" != "n" ]; then
	unset sha1sums
fi

subpkgs=('gnome-shell-extensions-dock')
subdescs=('dock for gnome-shell')
subrodepends=("$pkgname=$pkgver")
subdepends=('gnome-shell')
subgroups=('gnome')
subarchs=('i686 x86_64 ppc')

subpkgs=("${subpkgs[@]}" 'gnome-shell-extensions-theme')
subdescs=("${subdescs[@]}" 'themes user gnome-shell')
subrodepends=("${subrodepends[@]}" "$pkgname=$pkgver")
subdepends=("${subdepends[@]}" "gnome-shell")
subgroups=("${subgroups[@]}" 'gnome')
subarchs=("${subarchs[@]}" 'i686 x86_64 ppc')

build()	{
	if [ "$_F_gnome_git" != "n" ]; then
		Funpack_scm
		Fcd $pkgname
		sh ./autogen.sh
	else
		Fcd
		sh ./autogen.sh
	fi
	Fbuild --enable-extensions=all
	#dock too big 24 is enought
	Fsed "const DOCKICON_SIZE = 48" "const DOCKICON_SIZE = 24" \
		$Fdestdir/usr/share/gnome-shell/extensions/dock@gnome-shell-extensions.gnome.org/extension.js
	#hello world
	Frm usr/share/gnome-shell/extensions/example@gnome-shell-extensions.gnome.org
	Fsplit gnome-shell-extensions-theme usr/share/gnome-shell/extensions/user-theme@gnome-shell-extensions.gnome.org
	Fsplit gnome-shell-extensions-dock usr/share/gnome-shell/extensions/dock@gnome-shell-extensions.gnome.org
	Fbuild_gnome_scriptlet
}

# optimization OK
